{
  parserClass="com.github.xepozz.gitattributes.language.parser.AttributesParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Attributes"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xepozz.gitattributes.language.psi"
  psiImplPackage="com.github.xepozz.gitattributes.language.psi.impl"

  elementTypeHolderClass="com.github.xepozz.gitattributes.language.psi.AttributesTypes"
  elementTypeClass="com.github.xepozz.gitattributes.language.psi.AttributesElementType"
  tokenTypeClass="com.github.xepozz.gitattributes.language.psi.AttributesTokenType"

  psiImplUtilClass="com.github.xepozz.gitattributes.language.psi.impl.AttributesPsiImplUtil"
}

attributesFile ::= item_*

private item_ ::= (definition | COMMENT | EOL)

definition ::= pattern attributeList
pattern ::= TEXT
{
    implements=["com.intellij.psi.NavigatablePsiElement"]
    extends="com.github.xepozz.gitattributes.language.psi.impl.AttributesPatternBaseImpl"
    methods=[getReferences]
}
attributeList ::= attribute+
attribute ::= DASH? attribute_name (EQUALS_SIGN attribute_values)?
{
    implements=["com.intellij.psi.NavigatablePsiElement"]
    extends="com.github.xepozz.gitattributes.language.psi.impl.AttributesAttributeBaseImpl"
}
attribute_name ::= TEXT
attribute_values ::= attribute_value (COMMA attribute_value)*
attribute_value ::= TEXT (EQUALS_SIGN TEXT)?
